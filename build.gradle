/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// -----------------------------------------------------------------------------
// Main gradle build file for Spring Mobile Android
//
// - run `./gradlew(.bat) build` to kick off a complete compile-test-package
//
// @author Roy Clarkson
// -----------------------------------------------------------------------------


// -----------------------------------------------------------------------------
// Configuration for the root project
// -----------------------------------------------------------------------------
apply from: "$rootDir/gradle/version.gradle"
apply plugin: 'idea'

// used for artifact names, building doc upload urls, etc.
description = 'Spring Android'
abbreviation = 'SPRING-ANDROID'

// -----------------------------------------------------------------------------
// Configuration for all projects including this one (the root project)
//
// @see settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
allprojects {
    // group will translate to groupId during pom generation and deployment
    group = 'org.springframework.android'

    // version will be used in maven pom generation as well as determining
    // where artifacts should be deployed, based on release type of snapshot,
    // milestone or release.
    // @see org.springframework.build.Version under buildSrc/ for more info
    // @see gradle.properties for the declaration of this property.
    version = createVersion(springAndroidVersion)

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        mavenRepo urls: 'http://maven.springframework.org/snapshot'
        mavenCentral()
        mavenRepo urls: 'http://maven.springframework.org/release'
        mavenRepo urls: 'http://maven.springframework.org/milestone'
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/external'
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/release'
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/milestone'
    }
}


// -----------------------------------------------------------------------------
// Create collections of subprojects - each will receive their own configuration
// - all subprojects that start with spring-android-* are 'java projects'
// - documentation-related subprojects are not collected here
//
// @see configure(*) sections below
// -----------------------------------------------------------------------------

javaprojects = subprojects.findAll { project ->
    project.path.startsWith(':spring-android-')
}

// -----------------------------------------------------------------------------
// Configuration for all java subprojects
// -----------------------------------------------------------------------------
configure(javaprojects) {

    apply plugin: 'java' // tasks for conventional java lifecycle
    apply plugin: 'maven' // `gradle install` to push jars to local .m2 cache
    apply plugin: 'eclipse' // `gradle eclipse` to generate .classpath/.project
    apply plugin: 'idea' // `gradle idea` to generate .ipr/.iml

    // set up dedicated directories for jars and source jars.
    // this makes it easier when putting together the distribution
    libsBinDir = new File(libsDir, 'bin')
    libsSrcDir = new File(libsDir, 'src')

    // all core projects should be OSGi-compliant bundles
    // add the bundlor task to ensure proper manifests
    // apply from: "$rootDir/gradle/bundlor.gradle"
    apply from: "$rootDir/gradle/maven-deployment.gradle"

    jacksonVersion = '1.5.3'
    junitVersion = '4.7'
    mockitoVersion = '1.8.5'
    springVersion = '3.0.5.RELEASE'
	androidVersion = '1.5_r3'
	slf4jVersion = '1.6.1'
	slf4jAndroidVersion = '1.6.1-RC1'

    sourceSets {
        test {
            resources {
                srcDirs = ['src/test/resources', 'src/test/java']
            }
        }
    }

    // 'provided' dependencies should be transitive (e.g. servlet-api)
    configurations {
        provided { transitive = false }
    }

    sourceSets.main.compileClasspath += configurations.provided
    sourceSets.test.compileClasspath += configurations.provided
    sourceSets.test.runtimeClasspath += configurations.provided

    // dependencies that are common across all java projects
    dependencies {
		testCompile "junit:junit:$junitVersion"
		testCompile "org.mockito:mockito-all:$mockitoVersion"
		testCompile "org.springframework:spring-test:$springVersion"
    }

    // enable all compiler warnings (GRADLE-1077)
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

    // generate .classpath files without GRADLE_CACHE variable (GRADLE-1079)
    eclipseClasspath.variables = [:]
}


// -----------------------------------------------------------------------------
// Configuration for each individual core java subproject
//
// @see configure(javaprojects) above for general config
// -----------------------------------------------------------------------------
project('spring-android-commons-logging') {
    description = 'Spring Android Commons Logging'
    dependencies {
        compile ("com.google.android:android:$androidVersion") { provided = true }
    }
    
//	println "classpath = ${configurations.compile.collect {File file -> file.name}}"	
}

project('spring-android-rest-template') {
    description = 'Spring Android Rest Template'
    
	projBuildDir = "$buildDir/classes/main/"
	projLibDir = "$buildDir/exploded-jars/"
	    
    dependencies {
	    compile ("com.google.android:android:$androidVersion") { provided = true }
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:slf4j-android:$slf4jAndroidVersion"
        compile files(projLibDir)
	}
		
//	println "classpath = ${configurations.compile.collect {File file -> file.name}}"	
	
    // configuration for extracting required class files
    configurations { dependencyconf }
    dependencies {
        dependencyconf "org.springframework:spring-web:$springVersion"
    }
    
    // Required classes for RestTemplate support
    classFilter = [
		'org/springframework/core/NestedExceptionUtils.class',
		'org/springframework/core/NestedIOException.class',
		'org/springframework/core/NestedRuntimeException.class',
//    	'org/springframework/core/io/AbstractFileResolvingResource.class',
		'org/springframework/core/io/AbstractResource.class',
		'org/springframework/core/io/ByteArrayResource.class',
		'org/springframework/core/io/ClassPathResource.class',
		'org/springframework/core/io/InputStreamSource.class',
		'org/springframework/core/io/package-info.class',
		'org/springframework/core/io/Resource.class',
		'org/springframework/http/HttpEntity.class',
		'org/springframework/http/HttpHeaders.class',
		'org/springframework/http/HttpInputMessage.class',
		'org/springframework/http/HttpMessage.class',
		'org/springframework/http/HttpMethod.class',
		'org/springframework/http/HttpOutputMessage.class',
		'org/springframework/http/HttpStatus.class',
		'org/springframework/http/HttpStatus$Series.class',
//		'org/springframework/http/MediaType.class',
//		'org/springframework/http/MediaType$1.class',
//		'org/springframework/http/MediaType$2.class',
		'org/springframework/http/ResponseEntity.class',
		'org/springframework/http/client/AbstractClientHttpRequest.class',
		'org/springframework/http/client/ClientHttpRequest.class',
		'org/springframework/http/client/ClientHttpRequestFactory.class',
		'org/springframework/http/client/ClientHttpResponse.class',
		'org/springframework/http/client/SimpleClientHttpRequest.class',
		'org/springframework/http/client/SimpleClientHttpRequestFactory.class',
		'org/springframework/http/client/SimpleClientHttpResponse.class',
		'org/springframework/http/client/support/HttpAccessor.class',
		'org/springframework/http/converter/AbstractHttpMessageConverter.class',
		'org/springframework/http/converter/ByteArrayHttpMessageConverter.class',
//		'org/springframework/http/converter/FormHttpMessageConverter.class',
		'org/springframework/http/converter/HttpMessageConversionException.class',
		'org/springframework/http/converter/HttpMessageConverter.class',
		'org/springframework/http/converter/HttpMessageNotReadableException.class',
		'org/springframework/http/converter/HttpMessageNotWritableException.class',
		'org/springframework/http/converter/package-info.class',
//		'org/springframework/http/converter/ResourceHttpMessageConverter.class',
//		'org/springframework/http/converter/ResourceHttpMessageConverter$MultipartHttpOutputMessage.class',
		'org/springframework/http/converter/StringHttpMessageConverter.class',
		'org/springframework/http/converter/json/MappingJacksonHttpMessageConverter.class',
		'org/springframework/http/converter/json/package-info.class',
		'org/springframework/util/Assert.class',
//		'org/springframework/util/ClassUtils.class',
		'org/springframework/util/CollectionUtils.class',
		'org/springframework/util/CollectionUtils$EnumerationIterator.class',
		'org/springframework/util/FileCopyUtils.class',
		'org/springframework/util/LinkedCaseInsensitiveMap.class',
		'org/springframework/util/LinkedMultiValueMap.class',
		'org/springframework/util/MultiValueMap.class',
		'org/springframework/util/ObjectUtils.class',
		'org/springframework/util/ResourceUtils.class',
		'org/springframework/util/StringUtils.class',
		'org/springframework/web/client/DefaultResponseErrorHandler.class',
		'org/springframework/web/client/HttpClientErrorException.class',
		'org/springframework/web/client/HttpMessageConverterExtractor.class',
		'org/springframework/web/client/HttpServerErrorException.class',
		'org/springframework/web/client/HttpStatusCodeException.class',
		'org/springframework/web/client/package-info.class',
		'org/springframework/web/client/RequestCallback.class',
		'org/springframework/web/client/ResourceAccessException.class',
		'org/springframework/web/client/ResponseErrorHandler.class',
		'org/springframework/web/client/ResponseExtractor.class',
		'org/springframework/web/client/RestClientException.class',
		'org/springframework/web/client/RestOperations.class',
//		'org/springframework/web/client/RestTemplate.class',
//		'org/springframework/web/client/RestTemplate$AcceptHeaderRequestCallback.class',
//		'org/springframework/web/client/RestTemplate$HeadersExtractor.class',
//		'org/springframework/web/client/RestTemplate$HttpEntityRequestCallback.class',
//		'org/springframework/web/client/RestTemplate$HttpUrlTemplate.class',
//		'org/springframework/web/client/RestTemplate$ResponseEntityResponseExtractor.class',
		'org/springframework/web/util/package-info.class',
		'org/springframework/web/util/UriTemplate.class',
		'org/springframework/web/util/UriTemplate$Parser.class',
		'org/springframework/web/util/UriUtils.class'
		]
		
	// loops through spring framework jars, extracting and copying class files to the lib folder
	FileCollection jars = configurations.dependencyconf
	jars.each { jar ->
		zipTree(jar).visit { classFile ->
			if (classFile.relativePath.toString() in classFilter) {
				filePath = classFile.relativePath.getPathString().minus(classFile.name)
				copy {
					from classFile.file
					into projLibDir + filePath
				}
				copy {
					from classFile.file
					into projBuildDir + filePath
				}
			}
		}
	}
}

// add basic tasks like 'clean' and 'assemble' to the root project. e.g.: allows
// running `gradle clean` from the root project and deleting the build directory
apply plugin: 'base'

// add tasks like 'distArchive'
apply from: "$rootDir/gradle/dist.gradle"

// add tasks like 'snapshotDependencyCheck'
apply from: "${rootDir}/gradle/checks.gradle"


// -----------------------------------------------------------------------------
// Import tasks related to releasing and managing the project
// depending on the role played by the current user.
//
// @see gradle.properties for more information on roles
// -----------------------------------------------------------------------------
// add management tasks like `wrapper` for generating the gradlew* scripts
apply from: "$rootDir/gradle/wrapper.gradle"
