/*
 * Copyright 2010-2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.springframework.build.Version

// -----------------------------------------------------------------------------
// Main gradle build file for Spring Mobile
// @author Chris Beams
// @author Roy Clarkson
// -----------------------------------------------------------------------------

// -----------------------------------------------------------------------------
// Configuration for the root project
// -----------------------------------------------------------------------------
description = 'Spring Android'
abbreviation = 'ANDROID'

apply plugin: 'base'
apply plugin: 'idea'

def buildSrcDir = "$rootDir/buildSrc"
apply from: "$buildSrcDir/wrapper.gradle"
apply from: "$buildSrcDir/maven-root-pom.gradle"

// -----------------------------------------------------------------------------
// Configuration for all projects including this one (the root project)
// @see settings.gradle for list of all projects
// -----------------------------------------------------------------------------
allprojects {
    // group will translate to groupId during pom generation and deployment
    group = 'org.springframework.android'

    // version will be used in maven pom generation as well as determining
    // where artifacts should be deployed, based on release type of snapshot,
    // milestone or release.
    // @see org.springframework.build.Version under buildSrc/ for more info
    // @see gradle.properties for the declaration of this property.
    version = new Version(springAndroidVersion)

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        mavenRepo urls: 'http://maven.springframework.org/release'
        mavenRepo urls: 'http://maven.springframework.org/milestone'
        mavenRepo urls: 'http://maven.springframework.org/snapshot'
        mavenCentral()
    }
}


// -----------------------------------------------------------------------------
// Create collection of subprojects - each will receive their own configuration
// - all subprojects that start with spring-mobile-* are 'java projects'
// - documentation-related projects are not collected here
//
// @see configure(*) sections below
// -----------------------------------------------------------------------------
javaprojects = subprojects.findAll { project ->
    project.path.startsWith(':spring-android-')
}


// -----------------------------------------------------------------------------
// Configuration for all java projects
// -----------------------------------------------------------------------------
configure(javaprojects) {

    apply plugin: 'java'     // tasks for conventional java lifecycle
    apply plugin: 'maven'    // `gradle install` to push jars to local .m2 cache
    apply plugin: 'eclipse'  // `gradle eclipse` to generate .classpath/.project
    apply plugin: 'idea'     // `gradle idea` to generate .ipr/.iml

    // ensure JDK 5 compatibility
    sourceCompatibility=1.5
    targetCompatibility=1.5

    // set up dedicated directories for jars and source jars.
    // this makes it easier when putting together the distribution
    libsBinDir = new File(libsDir, 'bin')
    libsSrcDir = new File(libsDir, 'src')

    // add tasks for creating source jars and generating poms etc
    apply from: "$buildSrcDir/maven-deployment.gradle"

    // add tasks for finding and publishing .xsd files
    apply from: "$buildSrcDir/schema-publication.gradle"

    junitVersion = '4.7'
    log4jVersion = '1.2.16'
    springVersion = '3.1.0.M2'
    springSocialVersion = '1.0.0.RC2'
    springSecurityVersion = '3.1.0.RC2.crypto'
    androidVersion = '2.1_r1'
    commonsHttpclientVersion = '3.1'
    jacksonVersion = '1.8.4'
	gsonVersion = '1.7.1'
    simpleXmlVersion = '2.6'
    androidRomeVersion = '1.0.0-r2'
    javaxServletApiVersion = '2.5'
    commonsFileuploadVersion = '1.2'
    jettyVersion = '6.1.9'
    xmlUnitVersion = '1.3'
    
    // dependencies that are common across all java projects
    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "log4j:log4j:$log4jVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    // enable all compiler warnings (GRADLE-1077)
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

    // generate .classpath files without GRADLE_CACHE variable (GRADLE-1079)
    eclipseClasspath.variables = [:]
}


// -----------------------------------------------------------------------------
// Configuration for each individual core java subproject
//
// @see configure(javaprojects) above for general config
// -----------------------------------------------------------------------------
project('spring-android-core') {
    description = "Spring Android Core"
}

project('spring-android-auth') {
    description = 'Spring Android Auth'
    
    dependencies {
        compile ("com.google.android:android:$androidVersion") { 
            provided = true 
        }
        compile ("org.springframework.social:spring-social-core:$springSocialVersion") {
            // exclude spring-web in favor of spring-android-rest-template
            exclude group: "org.springframework", module: "spring-web"
        }
        compile ("org.springframework.security:spring-security-crypto:$springSecurityVersion") {
            transitive = false
        }
        compile project (':spring-android-core')
        compile project (':spring-android-rest-template');
    }
}

project('spring-android-rest-template') {
    description = 'Spring Android Rest Template'

    repositories {
        mavenRepo urls: 'https://android-rome-feed-reader.googlecode.com/svn/maven2/releases'
    }
    
    dependencies {
        compile ("com.google.android:android:$androidVersion") { 
            provided = true 
        }
        compile ("org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion") {
            optional = true
        }
        compile ("com.google.code.gson:gson:$gsonVersion") {
            optional = true
        }
        compile ("org.simpleframework:simple-xml:$simpleXmlVersion") { 
            optional = true 
            transitive = false 
        }
        compile ("com.google.code.android-rome-feed-reader:android-rome-feed-reader:$androidRomeVersion") { 
            optional = true 
        }
        compile ("commons-httpclient:commons-httpclient:$commonsHttpclientVersion") {
            optional = true
            transitive = false
        }
        compile project (':spring-android-core')

        testCompile "org.springframework:spring-core:$springVersion"
        testCompile "javax.servlet:servlet-api:$javaxServletApiVersion"
        testCompile "commons-fileupload:commons-fileupload:$commonsFileuploadVersion"
        testCompile "org.mortbay.jetty:jetty:$jettyVersion"
        testCompile "xmlunit:xmlunit:$xmlUnitVersion"
    }
}


// -----------------------------------------------------------------------------
// Configuration for the docs subproject
// -----------------------------------------------------------------------------
project('docs') {
    apply from: "$buildSrcDir/docs.gradle"
}

apply from: "$buildSrcDir/dist.gradle"
apply from: "$buildSrcDir/checks.gradle"